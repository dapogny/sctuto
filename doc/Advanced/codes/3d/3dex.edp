/* ****************************************************** */
/* ****************************************************** */
/*          Solution of a 3d Finite Element problem       */
/* ****************************************************** */
/* ****************************************************** */

load "tetgen"
load "msh3"
load "medit"

/* Set mesh and sol names */
string MESH   = "Lshape.mesh";
string SOL    = "Lshape.sol";
int    REFDIR  = 1;
int    REFNEU  = 2;
int    REFFREE = 0;

/* *************************************************** */
/*             Construction of the 3d mesh             */
/* *************************************************** */

int N = 20;

/* Creation of both angle faces */
border a(t=0,1.0) {x=0; y=1.0-t; label=0;};
border b(t=0,1.0) {x=t; y=0; label=0;};
border c(t=0,1.0) {x=1.0; y=t; label=0;};
border d(t=0,1.0) {x=1.0-t; y=1.0; label=0;};
border circ(t=0,1.0) {x=0.5+0.1*cos(2*pi*t); y=0.5+0.1*sin(2*pi*t); label=0;};

mesh Th2ds    = buildmesh(a(N) + b(N) + c(N) + d(N));
meshS Thang1  = movemesh23(Th2ds,transfo=[1.0+x,y,1.0]);
meshS Thang2  = movemesh23(Th2ds,transfo=[1.0,x,1.0+y]);

/* Creation of Dirichlet boundary */
int[int] rch  = [0,REFDIR]; // Set Dirichlet B.C. by turning ref 0 (default) to REFDIR
Th2ds         = change(Th2ds,region=rch);
meshS ThD     = movemesh23(Th2ds,transfo=[x,y,2.0]);

/* Creation of front face */
Th2ds        = buildmesh(a(N) + b(N) + c(N) + d(N) + circ(2*N));
rch          = [0,REFNEU,1,0]; // Change ref 0 (interior) to REFNEU and ref 1 (ext.) to 0
Th2ds        = change(Th2ds,region=rch);
meshS Thf    = movemesh23(Th2ds,transfo=[2.0,x,y]);

/* Creation of both L-shaped sides */
border Ll(t=0,2.0) {x=0; y=2.0-t; label=0;};
border Lb(t=0,2.0) {x=t; y=0; label=0;};
border Lr(t=0,1.0) {x=2.0; y=t; label=0;};
border Lang1(t=0,1.0) {x=2.0-t; y=1.0; label=0;};
border Lang2(t=0,1.0) {x=1.0; y=1.0+t; label=0;};
border Lt(t=0,1.0) {x=1.0-t; y=2.0; label=0;};

mesh Th2dL       = buildmesh(Ll(2*N) + Lb(2*N) + Lr(N) + Lang1(N) + Lang2(N) + Lt(N));
meshS ThSide1    = movemesh23(Th2dL,transfo=[x,0.0,y]);
meshS ThSide2    = movemesh23(Th2dL,transfo=[x,1.0,y],orientation=-1);

/* Creation of both rectangular sides */
border recta(t=0,1.0) {x=0; y=1.0-t; label=0;};
border rectb(t=0,2.0) {x=t; y=0; label=0;};
border rectc(t=0,1.0) {x=2.0; y=t; label=0;};
border rectd(t=0,2.0) {x=2.0-t; y=1.0; label=0;};

mesh Th2dRect    = buildmesh(recta(N) + rectb(2*N) + rectc(N) + rectd(2*N));
meshS ThBack     = movemesh23(Th2dRect,transfo=[0.0,y,x]);
meshS ThBot      = movemesh23(Th2dRect,transfo=[x,y,0.0],orientation=-1);

/* Total surface mesh */
meshS Tht = Thang1 + Thang2 + ThD + ThSide1 + ThBot + ThSide2 + ThBack + Thf;

/* Create 3d mesh */
real[int] dominfo = [0.5,0.5,0.5,0,0.0001];
mesh3 Th3 = tetg(Tht,switch="pqaAAYYQ",nbofregions=1,regionlist=dominfo);

/* Save mesh */
savemesh(Th3,MESH);

/* *************************************************** */
/*           Resolution of the Laplace equation        */
/* *************************************************** */

fespace Vh(Th3,P1);
Vh u,v;

func flux = -1.0;

problem Laplace(u,v,solver=CG) = int3d(Th3)(dx(u)*dx(v)+dy(u)*dy(v)+dz(u)*dz(v))
                                 - int2d(Th3,REFNEU)(flux*v)
                                 + on(REFDIR,u=0);
                                 
Laplace;

/* Save solution */
savesol(SOL,Th3,u);
